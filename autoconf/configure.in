dnl Copyright (c) 2003-2004 E. Will et al.
dnl Rights to this code are documented in doc/LICENSE.
dnl
dnl This file is the autoconf stub.
dnl
dnl $Id$

dnl Process this file with autoconf to produce a configure script.
AC_INIT

AC_CONFIG_AUX_DIR(autoconf)
AC_CONFIG_HEADER(inc/sysconf.h)
AC_PREFIX_DEFAULT(~/shrike)

dnl assert? «Stolen from H7's configure.in and modified»
AC_ARG_ENABLE(assert,
[  --enable-assert               Enable assert().
  --disable-assert              Disable assert(). ],
[
  case "$enableval" in
  yes)
    echo "Enabling assert() code."
    ASSERT=""
    ;;
  no)
    echo "Disabling assert() code."
    ASSERT="-DNDEBUG"
    ;;
  esac
])

dnl Checks for programs.
AC_PROG_MAKE_SET
AC_PROG_CC
AC_PROG_INSTALL
AC_EXEEXT
AC_PATH_PROG(RM, rm)
AC_PATH_PROG(MV, mv)
AC_PATH_PROG(CP, cp)
AC_C_INLINE

AC_MSG_CHECKING(how to generate dependancy info)
if test "$SunWorkShop" = "yes"; then
  AC_MSG_RESULT(Sun WorkShop/Forte using $CC -xM)
  MKDEP="$CC -xM"
fi
if test "$HPUX" = "yes"; then
  AC_MSG_RESULT(HP-UX C Compiler using makedepend)
  MKDEP="makedepend"
fi
if test "x$MKDEP" = "x"; then
  AC_MSG_RESULT(assuming $CC -MM)
  MKDEP="$CC -MM"
fi

AC_SUBST(MKDEP)

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(sys/time.h sys/select.h getopt.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T
AC_HEADER_TIME
AC_STRUCT_TM

dnl Checks for library functions.
AC_TYPE_SIGNAL
AC_FUNC_STRFTIME
AC_CHECK_FUNCS(select strdup strlcpy strlcat inet_pton inet_ntop gettimeofday umask mmap)
AC_CHECK_FUNC(socket,, AC_CHECK_LIB(socket, socket))
AC_CHECK_FUNC(gethostbyname,, AC_CHECK_LIB(nsl, gethostbyname))

AC_SUBST(ASSERT)

AC_OUTPUT(Makefile src/Makefile)
